apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.firebase-crash'
apply from: "../artifacts.gradle"

android {
    compileSdkVersion project.compileSdk
    buildToolsVersion project.toolsVersion
    defaultConfig {
        applicationId "com.babushka.slav_squad"
        minSdkVersion project.minSdk
        targetSdkVersion project.targetSdk
        versionCode 5
        versionName "1.0.4"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        //Required by com.wonderkiln:blurkit
        renderscriptTargetApi project.targetSdk
        renderscriptSupportModeEnabled true

        buildConfigField "long", "BUILD_TIMESTAMP", System.currentTimeMillis() + "L"
    }

    signingConfigs {
        release {
            keyAlias new File("../../keystores/releaseAlias").text.trim()
            keyPassword new File("../../keystores/releaseKeyPass").text.trim()
            storeFile new File("../../keystores/release.jks")
            storePassword new File("../../keystores/releaseStorePass").text.trim()
        }
        debug {
            keyAlias new File("../../keystores/debugAlias").text.trim()
            keyPassword new File("../../keystores/debugKeyPass").text.trim()
            storeFile new File("../../keystores/debug.jks")
            storePassword new File("../../keystores/debugStorePass").text.trim()
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "Slav Squad"
        }
        debug {
            signingConfig signingConfigs.debug
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            resValue "string", "app_name", "Slav Squad Debug"
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    //CORE
    compile deps.appCompat
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile deps.gson
    compile "com.android.support:recyclerview-v7:$project.supportLibVersion"
    compile "com.android.support:design:$project.supportLibVersion"

    //glide
    compile "com.github.bumptech.glide:glide:$rootProject.glideVersion"
    compile "com.android.support:support-v4:$rootProject.supportLibVersion"
    annotationProcessor "com.github.bumptech.glide:compiler:$rootProject.glideVersion"

    //3rd party UI
    compile 'de.hdodenhof:circleimageview:2.1.0'
    compile 'com.yqritc:recyclerview-flexibledivider:1.4.0'
    compile 'com.afollestad.material-dialogs:core:0.9.4.5'
    compile 'com.github.yalantis:ucrop:2.2.1'
    compile 'com.github.chrisbanes:PhotoView:2.0.0'
    compile 'com.wonderkiln:blurkit:1.0.0'

    //make life easy
    compile "com.jakewharton:butterknife:$project.butterKnifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$project.butterKnifeVersion"
    compile 'com.jakewharton.timber:timber:4.5.1'
    compile("com.github.hotchemi:permissionsdispatcher:$rootProject.permissionsDispatcherVersion") {
        // if you don't use android.app.Fragment you can exclude support for them
        exclude module: "support-v13"
    }
    annotationProcessor "com.github.hotchemi:permissionsdispatcher-processor:$rootProject.permissionsDispatcherVersion"

    //other
    compile 'org.greenrobot:eventbus:3.0.0'

    //extras
    debugCompile "com.squareup.leakcanary:leakcanary-android:$project.leakCanaryVersion"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$project.leakCanaryVersion"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$project.leakCanaryVersion"

    //Authentication
    compile "com.google.firebase:firebase-auth:$project.playServicesVersion"
    compile "com.google.android.gms:play-services-auth:$project.playServicesVersion"
    compile 'com.facebook.android:facebook-android-sdk:[4,5)'

    //Firebase
    compile "com.google.firebase:firebase-database:$project.playServicesVersion"
    compile "com.google.firebase:firebase-storage:$rootProject.playServicesVersion"
    compile "com.google.firebase:firebase-core:$rootProject.playServicesVersion"
    compile "com.google.firebase:firebase-messaging:$rootProject.playServicesVersion"
    compile "com.google.firebase:firebase-crash:$rootProject.playServicesVersion"
    compile "com.google.firebase:firebase-config:$rootProject.playServicesVersion"



    testCompile 'junit:junit:4.12'
}


apply plugin: 'com.google.gms.google-services'
